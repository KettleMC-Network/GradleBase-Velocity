import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java-library'
apply plugin: 'java'

libsDirName = System.getenv("libsDirName") == null ? libsDirName : System.getenv("libsDirName")
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs 'src'
        }

        resources {
            srcDirs 'resources'
        }
    }
    
    test {
    	java {
            srcDirs 'test'
        }
    }
}

repositories {
	mavenCentral()
	jcenter()
	
    flatDir {
        dirs 'libs'
    }
	
	maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    
    maven {
    	url 'https://m2.dv8tion.net/releases'
    }
    
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        url 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

configurations {
    internalLibs
    implementation.extendsFrom(internalLibs)
}

dependencies {
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.2")

	// GSON
    this.addModularCompile('com.googlecode.json-simple:json-simple:1.1.1', 'gson', false)
	
	// JDA
    this.addModularCompile('net.dv8tion:JDA:4.3.0_334', 'jda', false)
	
	// PlaceHolderAPI
    this.addModularCompile("me.clip:placeholderapi:2.10.10", "placeholderapi", false)
	
	// LuckPerms
    this.addModularCompile("net.luckperms:api:5.3", "luckperms", false)
	
	// Velocity
	this.addModularCompile("com.velocitypowered:velocity-api:3.0.1", "velocity", false)
	annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
	
	// BungeeCord Config
    this.addModularCompile("net.md-5:bungeecord-config:1.17-R0.1-SNAPSHOT", "bungee-config", false)

/*	
	// MySQL - Enable if needed
    this.addModularInternal("com.oracle.ojdbc:ojdbc8:19.3.0.0", "ojdbc", false)
    this.addModularInternal("mysql:mysql-connector-java:8.0.26", "mysql-connector", false)
*/

}

boolean checkLib(String filePath) {
    return file('libs/' + filePath + '.jar').exists()
}

void addModularCompile(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.implementation name: fileName
    else
        this.dependencies.implementation (repoName) { changing = changingB }
}

void addModularInternal(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.internalLibs name: fileName
    else
        this.dependencies.internalLibs (repoName) { changing = changingB }
}

jar {
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Class-Path': '.',
                'Main-Class': 'net.kettlemc.plugin.MainLauncher',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.internalLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from (sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [name: "PluginName"]
        filter ReplaceTokens, tokens: [version: "1.0.0-RELEASE"]
        filter ReplaceTokens, tokens: [author: "KettleMC"]
        filter ReplaceTokens, tokens: [main: "net.kettlemc.plugin.Main"]
    }
}

test {
	useJUnitPlatform()

	testLogging {
        exceptionFormat = 'full'
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}
